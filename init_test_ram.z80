	jp	test_ram

str_ramtest:
	defm	"TINY RAM TEST",0x00
str_slotfound:
	defm	"RAM IN SLOT: ",0x00
str_slotnotfound:
	defm	"NO RAM FOUND",0x00
str_success:
	defm	"SUCCESS!",0x00
str_failed:
	defm	"FAILED!",0x00

test_bytes:
	db	0x00, 0xff, 0xaa, 0x55, 0x00, 0xff, 0xaa, 0x55

test_ram:
	; Init VDP to Text Mode
	; VDP Register 0
	ld	a, %00000000
	out	(VDP_REG), a
	ld	a, 0 | 0x80
	out	(VDP_REG), a

	; VDP Register 1
	ld	a, %11010000
	out	(VDP_REG), a
	ld	a, 1 | 0x80
	out	(VDP_REG), a

	; VDP Register 2
	ld	a, %00000010
	out	(VDP_REG), a
	ld	a, 2 | 0x80
	out	(VDP_REG), a

	; VDP Register 4
	ld	a, %00000000
	out	(VDP_REG), a
	ld	a, 4 | 0x80
	out	(VDP_REG), a

	; VDP Register 7
	ld	a, VDP_COL_WHITE << 4 | VDP_COL_BLACK
	out	(VDP_REG), a
	ld	a, 7 | 0x80
	out	(VDP_REG), a


	; Copy font to VRAM
	ld	a, 0x08		; Lower eight bits of address
	out	(VDP_VRAM_ADDR), a
	ld	a, 0x01 | 0x40	; Upper eight bits of address
	out	(VDP_VRAM_ADDR), a

	ld	ix, font	; Index
	ld	hl, font_end - font	; Size
fntcpyloop:
	ld	a, (ix+0)		; Data
	out	(VDP_VRAM_DATA), a
	inc	ix
	dec	hl
	ld	a, h
	or	l
	jp	nz, fntcpyloop


	; Write "RAMTEST" to screen
	ld	a, 0x00
	out	(VDP_VRAM_ADDR), a
	ld	a, 0x08 | 0x40
	out	(VDP_VRAM_ADDR), a

	ld	hl, str_ramtest	; Load address of string into HL
strloop:
	ld	a, (hl)
	cp	0x00
	out	(VDP_VRAM_DATA), a
	inc	hl
	jp	nz, strloop


	; Find RAM slot
	ld	a, 0x00
	ld	b, 0x00		; Current primary slot/secondary slot
	ld	c, 0x00		; Amount of faults of B
	ld	d, 0x00		; Best primary slot/secondary slot
	ld	e, 0xff		; Amount of faults of D
	ld	ix, 0xe000	; Address to write to
testslt:
	ld	a, b		; Get primary slot
	rl	a
	rl	a
	rl	a
	rl	a
	and	%11000000	; Correct the format
	out	(PPI_PSLT), a	; Set primary slot

	ld	a, b		; Get secondary slot
	rl	a
	rl	a
	rl	a
	rl	a
	rl	a
	rl	a
	and	%11000000	; Correct the format
	ld	(SSLT), a	; Set secondary slot

	ld	(ix+0), 0x00	; Store a byte in (IX+0)
	ld	a, 0xff
	ld	a, (ix+0)	; Retrieve the same byte

	xor	0x00		; Compare byte
	ld	l, a

	ld	(ix+0), 0xff	; Store a byte in (IX+0)
	ld	a, 0x00
	ld	a, (ix+0)	; Retrieve the same byte

	xor	0xff		; Compare byte
	or	l		; Add faults together
	ld	l, a

	ld	h, 0x8
testslt_byteloop:		; Loop through byte and check for errors
	ld	a, l
	and	0x1
	cp	0x0		; Jump if not 0
	jp	z, testslt_byteloop_end
	inc	c		; Increase faults by one
testslt_byteloop_end:
	rr	l
	dec	h
	jp	nz, testslt_byteloop

	; Compare current to best
	ld	a, c
	cp	e
	jp	nc, testslt_next_slot
	ld	d, b
	ld	e, c

testslt_next_slot:
	inc	b		; Increase current primary/secondary slot
	ld	c, 0x00		; Reset fault counter
	ld	a, b
	cp	0x10
	jp	nz, testslt


	; slot is in D
foundslt:
	ld	a, 0x28
	out	(VDP_VRAM_ADDR), a
	ld	a, 0x08 | 0x40
	out	(VDP_VRAM_ADDR), a

	ld	hl, str_slotfound	; Load address of string into HL
foundsltstrloop:
	ld	a, (hl)
	cp	0x00
	out	(VDP_VRAM_DATA), a
	inc	hl
	jp	nz, foundsltstrloop

	ld	a, d
	rr	a
	rr	a
	and	%11
	add	a, 0x30
	out	(VDP_VRAM_DATA), a

	; Set primary slot
	ld	a, d
	rl	a
	rl	a
	rl	a
	rl	a
	and	%11000000
	out	(PPI_PSLT), a

	; Set secondary slot
	ld	a, d
	rl	a
	rl	a
	rl	a
	rl	a
	rl	a
	rl	a
	and	%11000000
	ld	(SSLT), a


	ld	iy, test_bytes
	ld	b, 0x8
	dec	b
ramtestinit:
	; Test upper 8KB of RAM with 0xaa
	ld	hl, 0xe000
	ld	de, 0x1fff
ramtestinitloop:
	ld	a, (iy+0)
	ld	(hl), a
	inc	hl
	dec	de
	ld	a, d
	or	e
	jp	nz, ramtestinitloop

ramtestloop:
	ld	hl, 0xe000
	ld	de, 0x1fff
ramtestlooprw:
	ld	a, (hl)
	cp	(iy+0)
	jp	nz, ramtestfailed

	ld	a, (iy+1)
	ld	(hl), a

	inc	hl
	dec	de
	ld	a, d
	or	e
	jp	nz, ramtestlooprw
ramtestend:
	inc	iy
	dec	b
	jp	nz, ramtestloop

	ld	hl, 0xe000
	ld	de, 0x1fff
ramtestfinalloop:
	ld	a, (hl)
	cp	(iy+0)
	jp	nz, ramtestfailed

	inc	hl
	dec	de
	ld	a, d
	or	e
	jp	nz, ramtestfinalloop


ramtestsuccess:
	dec	hl		; We did an extra increment
	; Write "SUCCESS" to screen
	ld	a, 0x50
	out	(VDP_VRAM_ADDR), a
	ld	a, 0x08 | 0x40
	out	(VDP_VRAM_ADDR), a

	ld	de, str_success	; Load address of string into HL
successstrloop:
	ld	a, (de)
	cp	0x00
	out	(VDP_VRAM_DATA), a
	inc	de
	jp	nz, successstrloop

	jp test_ram_end

ramtestfailed:
	ld	c, (iy+0)
	xor	c
	ex	af, af'
	; Write "FAILED" to screen
	ld	a, 0x50
	out	(VDP_VRAM_ADDR), a
	ld	a, 0x08 | 0x40
	out	(VDP_VRAM_ADDR), a

	ld	de, str_failed	; Load address of string into HL
failedstrloop:
	ld	a, (de)
	cp	0x00
	out	(VDP_VRAM_DATA), a
	inc	de
	jp	nz, failedstrloop

printbadaddress:
	ld	a, 0x78
	out	(VDP_VRAM_ADDR), a
	ld	a, 0x08 | 0x40
	out	(VDP_VRAM_ADDR), a

	ld	a, '0'
	out	(VDP_VRAM_DATA), a
	ld	a, 'x'
	out	(VDP_VRAM_DATA), a

	ld	e, 0x04
hexnumberloop:
	ld	a, h
	and	%11110000
	rr	a
	rr	a
	rr	a
	rr	a
	ld	b, a
	ld	a, 0x9
	cp	b
	ld	a, b
	jp	c, hexletter
	add	a, 0x30
	jp	hexoutput
hexletter:
	add	a, 0x37
hexoutput:
	out	(VDP_VRAM_DATA), a
	rl	l
	rl	h
	rl	l
	rl	h
	rl	l
	rl	h
	rl	l
	rl	h
	dec	e
	jp	nz, hexnumberloop


	ld	e, 0x08
	ld	a, 0xa0
	out	(VDP_VRAM_ADDR), a
	ld	a, 0x08 | 0x40
	out	(VDP_VRAM_ADDR), a
	ex	af, af'
	ld	b, a
printbrokenbyte:
	ld	a, b
	and	%10000000
	rlca
	add	a, 0x30
	rlc	b
	out	(VDP_VRAM_DATA), a
	dec	e
	jp	nz, printbrokenbyte

test_ram_end_failed:
	jp	test_ram_end_failed

test_ram_end:
	nop
